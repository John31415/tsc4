{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

(int) tlen (tuple t) asm "TLEN";
forall X -> (X) index(tuple t, int k) asm "INDEXVAR";

() recv_internal() {
}

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
    int n = matrixA.tlen();
    int m = matrixB.tlen();
    int p = matrixB.index(0).tlen();
    int i = 0;
    tuple solve = empty_tuple();
    while(i < n){
        tuple t = empty_tuple();
        int j = 0;
        while(j < p){
            int s = 0;
            int k = 0;
            while(k < m){
                s += matrixA.index(i).index(k) * matrixB.index(k).index(j); 
                k += 1;
            }
            t~tpush(s);
            j += 1;
        }
        solve~tpush(t);
        i += 1;
    }
    return solve;
}

